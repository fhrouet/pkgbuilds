# Maintainer: fhrouet <fhrouet [at] msn [dot] com>

pkgname=openblas
_pkgname=OpenBLAS
pkgver=0.2.15
pkgrel=1
pkgdesc="Optimized BLAS+LAPACK library"
arch=('i686' 'x86_64')
url="http://xianyi.github.com/OpenBLAS/"
license=('BSD')
depends=('gcc-libs')
makedepends=('perl' 'gcc-fortran')
provides=('openblas' 'blas=3.5.0' 'lapack=3.5.0' 'cblas=3.5.0')
conflicts=('openblas' 'blas' 'lapack' 'cblas')
source=(http://github.com/xianyi/OpenBLAS/archive/v${pkgver}.tar.gz)
sha256sums=('73c40ace5978282224e5e122a41c8388c5a19e65a6f2329c2b7c0b61bacc9044')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  make USE_OPENMP=1
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR="${pkgdir}" install

  # libblas, liblapack, libcblas all point to libopenblas
  cd "${pkgdir}/usr/lib/"
  ln -sf libopenblas.so libblas.so
  ln -sf libopenblas.so libblas.so.3
  ln -sf libopenblas.so libblas.so.3.5.0
  ln -sf libopenblas.so libcblas.so
  ln -sf libopenblas.so liblapack.so
  ln -sf libopenblas.so liblapack.so.3
  ln -sf libopenblas.so liblapack.so.3.5.0
  rm -rf cmake
}
